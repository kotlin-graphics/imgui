import org.gradle.internal.os.OperatingSystem

apply plugin: 'java'
apply plugin: 'kotlin'

//apply plugin: 'application'
//mainClassName = "imgui.ImguiKt"
apply plugin: 'com.github.johnrengelman.shadow'

// jitpack
apply plugin: 'maven'
group = 'com.github.kotlin-graphics'

buildscript {

    ext.kotlinVersion = '1.2.41'

    repositories {
        jcenter()   // shadow
        mavenCentral()
        maven { setUrl("https://dl.bintray.com/kotlin/kotlin-eap") }
        maven { setUrl("https://oss.sonatype.org/content/repositories/snapshots/") }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.3'
        classpath "org.junit.platform:junit-platform-gradle-plugin:1.1.0"
    }
}

dependencies {

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    compile 'com.github.kotlin-graphics:uno-sdk:6db5c2919e60af45389b5f255d1c0a29c29f7ef6'

    testCompile 'io.kotlintest:kotlintest-runner-junit5:3.0.6'

    def joglVersion = '2.3.2'
    testCompile "org.jogamp.gluegen:gluegen-rt-main:$joglVersion"
    testCompile "org.jogamp.jogl:jogl-all-main:$joglVersion"

    switch (OperatingSystem.current()) {
        case OperatingSystem.WINDOWS:
            ext.lwjglNatives = "natives-windows"
            break
        case OperatingSystem.LINUX:
            ext.lwjglNatives = "natives-linux"
            break
        case OperatingSystem.MAC_OS:
            ext.lwjglNatives = "natives-macos"
            break
    }
    ext.lwjglVersion = "3.1.7-SNAPSHOT"
    compile "org.lwjgl:lwjgl:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-glfw:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-openal:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-opengl:${lwjglVersion}"
    compile "org.lwjgl:lwjgl-stb:${lwjglVersion}"
    [
            "org.lwjgl:lwjgl:${lwjglVersion}:${lwjglNatives}",
            "org.lwjgl:lwjgl-glfw:${lwjglVersion}:${lwjglNatives}",
            "org.lwjgl:lwjgl-jemalloc:${lwjglVersion}:${lwjglNatives}",
            "org.lwjgl:lwjgl-openal:${lwjglVersion}:${lwjglNatives}",
            "org.lwjgl:lwjgl-opengl:${lwjglVersion}:${lwjglNatives}",
            "org.lwjgl:lwjgl-stb:${lwjglVersion}:${lwjglNatives}"
    ].each {
        testRuntime it
        shadow it
    }

    compile files("${System.properties['java.home']}/../lib/tools.jar")
}

repositories {
    mavenCentral()
    maven { setUrl("https://dl.bintray.com/kotlin/kotlin-eap") }
    maven { setUrl("https://oss.sonatype.org/content/repositories/snapshots/") }
    maven { setUrl('https://jitpack.io') }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

shadowJar {
    exclude 'module-info.class'
}